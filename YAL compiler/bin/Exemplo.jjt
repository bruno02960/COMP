options
{
  LOOKAHEAD= 1;

}

PARSER_BEGIN(Exemplo)
import java.io.FileInputStream;
import java.io.IOException;

//código Java que invoca o parser 
public class Exemplo
{
  public static void main(String args []) throws ParseException, IOException
  {
    //criação do objecto
    Exemplo parser = new Exemplo(new FileInputStream("src/test.yal"));
    SimpleNode root = parser.Module();
    root.dump("");
  }
}

PARSER_END(Exemplo)

// definição dos tokens (símbolos terminais) 
< DEFAULT >
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      "*"
    | ~[ "*", "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

/* reserved words */
< DEFAULT >
TOKEN :
{
  < RELA_OP :
    ">"
  | "<"
  | "<="
  | ">="
  | "=="
  | "!=" >
| < ADDSUB_OP :
    "+"
  | "-" >
| < ARITH_OP :
    "*"
  | "/"
  | "<<"
  | ">>"
  | ">>>" >
| < BITWISE_OP :
    "&"
  | "|"
  | "^" >
| < NOT_OP : "!" >
| < WHILE : "while" >
| < IF : "if" >
| < ELSE : "else" >
| < ASSIGN : "=" >
| < ASPA : "\"" >
| < LPAR : "(" >
| < RPAR : ")" >
| < VIRG : "," >
| < PVIRG : ";" >
| < LCHAVETA : "{" >
| < RCHAVETA : "}" >
| < FUNCTION : "function" >
| < MODULE : "module" >
| < SIZE : "size" >
| < LPARRETO : "[" >
| < RPARRETO : "]" >
| < PONTO : "." >
}

< DEFAULT >
TOKEN :
{
  < INTEGER : (< DIGIT >)+ >
| < ID :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| < #LETTER : [ "$", "A"-"Z", "_", "a"-"z" ] >
| < #DIGIT : [ "0"-"9" ] >
| < STRING : "\"" ([ "a"-"z", "A"-"Z", "0"-"9", ":", " ", "=" ])+ "\"" >
}

//Non-terminals
/* Module ::= <MODULE> <ID> <LCHAVETA> ( Declaration )* ( Function )* <RCHAVETA> */
SimpleNode Module() #MODULE :
{Token t;}
{
  try
  {
    < MODULE > t = < ID >
    {
      jjtThis.value = t.image;
    } < LCHAVETA > (Declaration()) * (Function()) * < RCHAVETA >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RCHAVETA);
  }
  {
    return jjtThis;
  }
}

/* Declaration ::= 	( ArrayElement | ScalarElement )
					(
					  		<ASSIGN>
							( 	( "[" ArraySize "]" )
							| (<ADDSUB_OP> )? <INTEGER> )
															)? <PVIRG> */
void Declaration() #DECLARATION() :
{Token t;}
{
  try
  {
    (LOOKAHEAD(2)
    ArrayElement()
  | ScalarElement()) (< ASSIGN >
    ((< LPARRETO >
    ArraySize() < RPARRETO >)
  | (t = < ADDSUB_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }) ? t = < INTEGER >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    })) ? < PVIRG >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != PVIRG);
  }
}

/* Function ::= ( ( <FUNCTION> ( ArrayElement | ScalarElement ) <ASSIGN> <ID> <LPAR> (Varlist )? <RPAR> ) | ( <FUNCTION> <ID> <LPAR> ( Varlist )? <RPAR> ) )<LCHAVETA> Stmtlst <RCHAVETA> */
void Function() #FUNCTION :
{Token t;}
{
  try
  {
    (LOOKAHEAD(3)
    (< FUNCTION > (LOOKAHEAD(2)
    ArrayElement()
  | ScalarElement()) < ASSIGN > t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } < LPAR > (Varlist()) ? < RPAR >)
  | (< FUNCTION > t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } < LPAR > (Varlist()) ? < RPAR >)) < LCHAVETA > Stmtlst() < RCHAVETA >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RCHAVETA);
  }
}

/* Varlist ::= ( ArrayElement | ScalarElement ) ( <VIRG> ( ArrayElement | ScalarElement ))* */
void Varlist() #void :
{}
{
    (LOOKAHEAD(2)
    ArrayElement()
  | ScalarElement()) (< VIRG >
     (LOOKAHEAD(2)
    ArrayElement()
  | ScalarElement())) *
}

/* ArrayElement ::= <ID> "[" "]" */
void ArrayElement() #ARRAYELEMENT :
{
  Token t;
}
{
  try
  {
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } < LPARRETO > < RPARRETO >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RPARRETO);
  }
}

/*ScalarElement ::= <ID>*/
void ScalarElement() #SCALARELEMENT :
{
  Token t;
}
{
  try
  {
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != ID);
  }
}

/* Stmtlst ::= ( Stmt )* */
void Stmtlst() #void :
{}
{
    (Stmt()) *
}

/* Stmt ::= While | If | Assign | Call <PVIRG> */
void Stmt() #void :
{}
{
  try
  {
    While()
  | If()
  | LOOKAHEAD(3)
    Assign()
  | Call() < PVIRG >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != PVIRG);
  }
}

/* Assign ::= Lhs <ASSIGN> Rhs <PVIRG> */
void Assign() #void :
{}
{
  try
  {
    Lhs() < ASSIGN > Rhs() < PVIRG >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != PVIRG);
  }
}

/* Lhs ::= ArrayAccess | ScalarAccess */
void Lhs() #void :
{}
{
    LOOKAHEAD(2)
    ArrayAccess()
  | ScalarAccess()
}

/* Rhs ::= ( Term ( ( <ARITH_OP> | <BITWISE_OP> | <ADDSUB_OP> ) Term )? ) | "[" ArraySize "]" */
void Rhs() #RHS :
{ Token t; }
{
  try
  {
    (
    Term() 
    (
    (
    t = < ARITH_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | t = < BITWISE_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | t = < ADDSUB_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
    ) 
    Term()
    ) ?
    )
  |  < LPARRETO > ArraySize() < RPARRETO >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RPARRETO);
  }
}

/* ArraySize ::= ScalarAccess | <INTEGER> */
void ArraySize() #ARRAYSIZE :
{
  Token t;
}
{
    ScalarAccess()
  | t = < INTEGER >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
}

/* Term ::= ( <ADDSUB_OP> )? ( <INTEGER> | Call | ArrayAccess | ScalarAccess ) */
void Term() #TERM :
{
  Token t;
}
{
    (t = < ADDSUB_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
    ) ? 
    (
    t = < INTEGER >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | LOOKAHEAD(3)
    Call()
  | LOOKAHEAD(2)
    ArrayAccess()
  | ScalarAccess()
    )
}

/* Exprtest ::= <LPAR> Lhs <RELA_OP> Rhs <RPAR> */
void Exprtest() #EXPRTEST :
{ Token t; }
{
  try
  {
     < LPAR > Lhs() t = < RELA_OP >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } Rhs() < RPAR >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RPAR);
  }
}

/*While ::= <WHILE> Exprtest <LCHAVETA> Stmtlst <RCHAVETA> */
void While() #WHILE :
{}
{
  try
  {
    < WHILE > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RCHAVETA);
  }
}

/*If ::= <IF> Exprtest <LCHAVETA> Stmtlst <RCHAVETA> ( <ELSE> <LCHAVETA> Stmtlst <RCHAVETA> )? */
void If() #IF :
{}
{
  try
  {
    < IF > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
    (
    < ELSE > < LCHAVETA > Stmtlst() < RCHAVETA >
    ) ?
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RCHAVETA);
  }
}

/*Call ::= <ID> ( "." <ID> )? <LPAR> ( ArgumentList )? <RPAR> */
void Call() #CALL :
{ Token t;}
{
  try
  {
    t = < ID >
    {
      jjtThis.value = t.image;
    } (< PONTO >
    {
      jjtThis.value += ".";
    } t = < ID >
    {
      jjtThis.value += t.image;
    }) ? < LPAR > 
    (
    ArgumentList()
    ) ? 
    < RPAR >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RPAR);
  }
}

/* ArgumentList ::= Argument ( <VIRG> Argument )* */
void ArgumentList() #void :
{}
{
    Argument() 
    (
    < VIRG > Argument()
    ) *
}

/* Argument ::= ( <ID> | <STRING> | <INTEGER> ) */
void Argument() #ARGUMENT :
{
  Token t;
}
{
    (
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | t = < STRING >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | t = < INTEGER >	
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
    )
}

/* ArrayAccess ::= <ID> "[" Index "]"*/
void ArrayAccess() #ARRAYACCESS :
{Token t;}
{
  try
  {
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } < LPARRETO > Index() < RPARRETO >
  }
  catch (ParseException e)
  {
    System.out.println(e.toString());
    Token t2;
    do
    {
      t2 = getNextToken();
    }
    while (t2.kind != RPARRETO);
  }
}

/* ScalarAccess ::= <ID> ( "." <SIZE> )? */
void ScalarAccess() #SCALARACCESS :
{
  Token t;
}
{
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    } (< PONTO > < SIZE >) ?
}

/*Index ::= <ID> | <INTEGER>*/
void Index() #INDEX :
{
  Token t;
}
{
    t = < ID >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
  | t = < INTEGER >
    {
      if(jjtThis.value != "")   jjtThis.value += " " + t.image;   else   jjtThis.value += t.image;
    }
}
